<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" borderStyle="solid" width="100%" height="100%"
	minHeight="150" dataChange="dataChangeHandler(event)" doubleClickEnabled="true" doubleClick="doubleClickHandler(event)">
	
	<mx:Script>
		<![CDATA[
			import SearchLogic.Query;
			import SearchLogic.QueryHistory;
			import mx.controls.Alert;
			import mx.controls.List;
			import mx.core.IFlexDisplayObject;
			import mx.events.CloseEvent;
			import mx.containers.TitleWindow;
			import mx.managers.PopUpManager;
			import mx.collections.ArrayCollection;
			import mx.core.Application;
		
			[Bindable]
			private var linkButtonLabel:String = "";
			
			//this array is filled with Object with two attributes:
			//Object.parent - the SearchResponse
			//Object.newsresult - the NewsResult
			[Bindable]
			private var relatedSearches:ArrayCollection;
		
			private function dataChangeHandler(event:Event):void
			{
				if(data == null)
					return;
				var dict:Dictionary = Query.urlHash;
				if (dict[data.Url] != undefined)
				{
					relatedSearches = dict[data.Url];					//TODO: Some tweaks need to be made here so that the LinkButton
					//is not enabled when the only match is from the search itself,
					//since every search is also in the Hash, thus every item will
					//have a similar search, itself, which is confusing to the user.
					if (relatedSearches.length > 1)
					{
						linkButton.visible = true;
						linkButton.enabled = true;
						var labelText:String = relatedSearches.length + " other ";
						labelText += relatedSearches.length == 2 ? "match" : "matches";
						labelText += " found";
						linkButtonLabel = labelText;
					}
					else
					{
						linkButton.visible = false;
						linkButton.enabled = false;
						linkButtonLabel = "";
					}
				}
				else
				{
					linkButton.visible = false;
					linkButton.enabled = false;
					linkButtonLabel = "";
				}
			}
			
			private function doubleClickHandler(event:MouseEvent):void
			{
				var urlRequest:URLRequest = new URLRequest(event.currentTarget.data.Url);
                navigateToURL(urlRequest, "_blank");
			}
			
			/**
			 * This is the handler for when the similar search link button is clicked.
			 */
			private function linkButtonClickHandler(event:Event):void
			{
				var similarList:List = new List();
				similarList.dataProvider = relatedSearches;
				similarList.percentHeight = 100;
				similarList.percentWidth = 100;
				similarList.doubleClickEnabled = true;
				similarList.columnCount = 2;
				similarList.rowCount = 2;
				similarList.itemRenderer = new ClassFactory(SimilarSearchItemRenderer);
				var tw:TitleWindow = new TitleWindow();
				tw.title = "Similar Searches";
				tw.showCloseButton = true;
				tw.addChild(similarList);
				tw.addEventListener(CloseEvent.CLOSE, removeSimilarSearchPopUp);
				PopUpManager.addPopUp(tw, this);
				PopUpManager.centerPopUp(tw);
			}
			
			/**
			 * Remove the PopUpWindow when the user clicks the X
			 */ 
			private function removeSimilarSearchPopUp(event:CloseEvent):void
			{
				PopUpManager.removePopUp(IFlexDisplayObject(event.currentTarget));
			}
		]]>
	</mx:Script>
	
	<mx:Text text="{data.Title}" selectable="false" width="100%" truncateToFit="true" styleName="ArticleTitle"/>
	<mx:LinkButton id="linkButton" label="{linkButtonLabel}" click="linkButtonClickHandler(event)" styleName="SimilarSearchLinkButton"/>
	<mx:TextArea text="{data.Snippet}" selectable="false" editable="false" width="100%" height="100%" styleName="ArticleSnippet"/>
</mx:VBox>
